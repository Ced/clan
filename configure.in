dnl
dnl   /**------- <| --------------------------------------------------------**
dnl    **         A                     Clan                                **
dnl    **---     /.\   -----------------------------------------------------**
dnl    **   <|  [""M#               configure.in                            **
dnl    **-   A   | #   -----------------------------------------------------**
dnl    **   /.\ [""M#         First version: 30/04/2008                     **
dnl    **- [""M# | #  U"U#U  -----------------------------------------------**
dnl         | #  | #  \ .:/
dnl         | #  | #___| #
dnl ******  | "--'     .-"  ***************************************************
dnl *     |"-"-"-"-"-#-#-##   Clan : the Chunky Loop Analyser (experimental)  *
dnl ****  |     # ## ######  **************************************************
dnl *      \       .::::'/                                                    *
dnl *       \      ::::'/     Copyright (C) 2008 University Paris-Sud 11      *
dnl *     :8a|    # # ##                                                      *
dnl *     ::88a      ###      This is free software; you can redistribute it  *
dnl *    ::::888a  8a ##::.   and/or modify it under the terms of the GNU     *
dnl *  ::::::::888a88a[]:::   Lesser General Public License as published by   *
dnl *::8:::::::::SUNDOGa8a::. the Free Software Foundation, either version    *
dnl *::::::::8::::888:Y8888::                2.1 of the License, or (at your  *
dnl *::::':::88::::888::Y88a::::::::::::...  option) any later version.       *
dnl *::'::..    .   .....   ..   ...  .                                       *
dnl * This software is distributed in the hope that it will be useful, but    *
dnl * WITHOUT ANY WARRANTY; without even the implied warranty of              *
dnl * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU       *
dnl * General Public License  for more details.	                              *
dnl *                                                                         *
dnl * You should have received a copy of the GNU Lesser General Public        *
dnl * License along with software; if not, write to the Free Software         *
dnl * Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307    *
dnl * USA                                                                     *
dnl *                                                                         *
dnl * Clan, the Chunky Loop Analyser                                          *
dnl * Written by Cedric Bastoul, Cedric.Bastoul@u-psud.fr                     *
dnl *                                                                         *
dnl ***************************************************************************/
dnl
dnl Input file for autoconf to build a configuration shellscript.


AC_PREREQ(2.13)
dnl Fill here the @bug email adress.
AC_INIT([clan], [0.7.0], [cedric.bastoul@u-psud.fr,pouchet@cse.ohio-state.edu])
dnl A common file, which serve as a test.
AC_CONFIG_SRCDIR([include/clan/symbol.h])
dnl Put as most as possible configuration files to an auxialiry
dnl directory.
AC_CONFIG_AUX_DIR([autoconf])
dnl Initialize automake. Here, a special tar version that enables
dnl (very) long filenames.
AM_INIT_AUTOMAKE([1.9 tar-ustar no-define foreign dist-bzip2])

dnl default version
BITS="64"
OPENSCOP_RELEASE="0.3.0"
CLAN_INT_T=CLAN_INT_T_IS_LONGLONG


dnl /**************************************************************************
dnl  *                              Checking                                  *
dnl  **************************************************************************/


dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_CHECK_PROG(CD, cd)
AC_PROG_LIBTOOL
AC_PROG_LEX
AC_PROG_YACC
AC_CHECK_PROGS(DOXYGEN,doxygen,doxygen)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([errno.h stddef.h stdlib.h string.h strings.h unistd.h])

dnl Checks for library functions.
AC_CHECK_FUNCS(strtol)


dnl /**************************************************************************
dnl  *                             Option setting                             *
dnl  **************************************************************************/

AC_ARG_ENABLE(internal-openscop,
	AC_HELP_STRING([--enable-internal-openscop],
              	           [Use shipped ScopLib]),
	[ if test "x$enableval" = xno; then 
	    shipped_openscop=no;
          else
	    shipped_openscop=yes;
          fi
	], [ shipped_openscop=uncertain ])


dnl Offer --with-openscop.
AC_ARG_WITH(openscop,
	    AC_HELP_STRING([--with-openscop=DIR],
              	           [DIR Location of OPENSCOP package]),
            [if test "x$shipped_openscop" != xyes; then 
	        with_openscop=$withval;
	        CPPFLAGS="${CPPFLAGS} -I$withval/include";
	        LDFLAGS="${LDFLAGS} -L$withval/lib";
		shipped_openscop=no;
	     fi;
	    ],
            [if test "x$shipped_openscop" = xno; then 
	    	with_openscop=yes;
	     else
	     	with_openscop=check;
	     fi;
	     ])
dnl Check for openscop existence.
AS_IF([test "x$with_openscop" != xno ],
      [AC_CHECK_LIB([openscop], [openscop_scop_read],
      [LIBS="-lopenscop $LIBS";
       AC_DEFINE([HAVE_LIBOPENSCOP], [1], [Define if you have libopenscop])
      ],
      [if test "x$with_openscop" != xcheck; then
       AC_MSG_FAILURE([Test for OPENSCOP failed. Use --with-openscop to specify libopenscop path.]);
       fi;
      ])
])

AS_IF([test "x$shipped_openscop" != xno ], 
      [abs_top_srcdir=`cd $srcdir; pwd`;
       abs_top_builddir=`pwd`;
       if ! test -f $abs_top_srcdir/openscop/openscop-$OPENSCOP_RELEASE ; then
       	  cd $abs_top_srcdir/openscop && tar xzf openscop-$OPENSCOP_RELEASE.tar.gz ; cd -;
       fi;
       AC_CONFIG_SUBDIRS([openscop/openscop-$OPENSCOP_RELEASE])
       BUILD_OPENSCOP="openscop/openscop-$OPENSCOP_RELEASE";
       CPPFLAGS="${CPPFLAGS} -I$abs_top_srcdir/openscop/openscop-$OPENSCOP_RELEASE/include -I$abs_top_builddir/openscop/openscop-$OPENSCOP_RELEASE/include";
       LDFLAGS="${LDFLAGS} -L$abs_top_builddir/openscop/openscop-$OPENSCOP_RELEASE/source";
       LIBS="-lopenscop $LIBS";
      ],
      [BUILD_OPENSCOP=""])


dnl /**************************************************************************
dnl  *                            Substitutions                               *
dnl  **************************************************************************/


dnl Substitutions to do.
AC_SUBST(ac_aux_dir)
AC_SUBST(abs_top_srcdir)
AC_SUBST(BUILD_OPENSCOP)

dnl Configure Makefiles.
AC_CONFIG_FILES([
	Makefile
	doc/Makefile
	doc/Doxyfile
	include/Makefile
	include/clan/clan.h
	source/Makefile
	tests/Makefile
	],
	[test -z "$CONFIG_HEADERS" || echo timestamp > source/stamp-h.in])

AC_OUTPUT


echo "             /*-----------------------------------------------*"
echo "              *           Clan configuration is OK            *"
echo "              *-----------------------------------------------*/"
echo "It appears that your system is OK to start Clan compilation. You need"
echo "now to type \"make\". Lastly type \"make install\" to install Clan on"
echo "your system (log as root if necessary)."
